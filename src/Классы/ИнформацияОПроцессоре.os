// BSLLS:ExportVariables-off

#Использовать "../internal"

Перем ИмяПроцессора Экспорт; // Строка
Перем КоличествоПроцессоров Экспорт; // Число
Перем КоличествоЯдер Экспорт; // Число
Перем КоличествоЛогическихПроцессоров Экспорт; // Число
Перем НоминальнаяЧастота Экспорт; // Число
Перем МаксимальнаяЧастота Экспорт; // Число
Перем Архитектура Экспорт; // см. АрхитектурыПроцессоров

// Конструктор класса.
//
// Параметры:
//   ЗаполнитьДанными - Булево - Признак автоматического сбора информации о процессоре при создании объекта
//                               Если Истина, данные о процессоре собираются и кэшируются.
//                               Если Ложь, свойства остаются неинициализированными.
//                               По умолчанию: Истина.
//
// Вызывает исключение:
//   Если не удалось выполнить команду сбора информации о процессоре
Процедура ПриСозданииОбъекта(ЗаполнитьДанными = Истина)
	
	ИмяПроцессора = "";
	КоличествоПроцессоров = 0;
	КоличествоЯдер = 0;
	КоличествоЛогическихПроцессоров = 0;
	НоминальнаяЧастота = 0;
	МаксимальнаяЧастота = 0;
	Архитектура = Неопределено;

	Если ЗаполнитьДанными Тогда
		ЗаполнитьДаннымиОПроцессоре();
	КонецЕсли;

КонецПроцедуры

// Возвращает полное описание характеристик процессора.
//
// Возвращаемое значение:
//   Строка
Функция ПолноеОписание() Экспорт
	Возврат ПодготовкаОписанияПроцессора.ПолноеОписание(ЭтотОбъект);
КонецФункции

// Возвращает краткое описание характеристик процессора.
//
// Возвращаемое значение:
//   Строка
Функция КраткоеОписание() Экспорт
	Возврат ПодготовкаОписанияПроцессора.КраткоеОписание(ЭтотОбъект);
КонецФункции

Процедура ЗаполнитьДаннымиОПроцессоре()

	Попытка
		ИнформацияОПроцессоре = ДетекторПроцессора.Определить();
		Если Не ИнформацияОПроцессоре = Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ИнформацияОПроцессоре);
		КонецЕсли;		
	Исключение
		ВызватьИсключение "Не удалось заполнить данными о процессоре: " + КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;

КонецПроцедуры