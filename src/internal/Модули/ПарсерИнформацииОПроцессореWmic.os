Функция Распарсить(ВыводWmic) Экспорт
	
	ИнформацияОПроцессоре = Новый ИнформацияОПроцессоре(Ложь);
	ИменаМоделей = Новый Массив();
	СуммаМаксимальныхЧастот = 0;

	ИсправитьВыводWmic(ВыводWmic);
	Процессоры = ПарсерСекцийИнформацииОПроцессоре.РаспарситьСекции(ВыводWmic, "=");
	Для Каждого Процессор Из Процессоры Цикл

		КоличествоЯдер = КонвертацияЗначенийCpuInfo.СтрокаВЧисло(Процессор["NumberOfCores"]);
		Если КоличествоЯдер > 0 Тогда
			ИнформацияОПроцессоре.КоличествоЯдер = ИнформацияОПроцессоре.КоличествоЯдер + КоличествоЯдер;
		КонецЕсли;

		КоличествоЛогическихПроцессоров = КонвертацияЗначенийCpuInfo.СтрокаВЧисло(Процессор["NumberOfLogicalProcessors"]);
		Если КоличествоЛогическихПроцессоров > 0 Тогда
			ИнформацияОПроцессоре.КоличествоЛогическихПроцессоров = 
				ИнформацияОПроцессоре.КоличествоЛогическихПроцессоров + КоличествоЛогическихПроцессоров;
		КонецЕсли;

		ИмяМодели = Процессор["Name"];
		Если ЗначениеЗаполнено(ИмяМодели) Тогда
			Если ИменаМоделей.Найти(ИмяМодели) = Неопределено Тогда
				ИменаМоделей.Добавить(ИмяМодели);
			КонецЕсли;
			
			ИнформацияОПроцессоре.КоличествоПроцессоров = ИнформацияОПроцессоре.КоличествоПроцессоров + 1;
		КонецЕсли;

		МаксимальнаяЧастота = КонвертацияЗначенийCpuInfo.СтрокаВЧисло(Процессор["MaxClockSpeed"]);
		Если МаксимальнаяЧастота > 0 Тогда
			СуммаМаксимальныхЧастот = СуммаМаксимальныхЧастот + МаксимальнаяЧастота;
		КонецЕсли;

		Архитектура = АрхитектураПоКоду(Процессор["Architecture"]);
		Если ЗначениеЗаполнено(Архитектура) Тогда
			ИнформацияОПроцессоре.Архитектура = Архитектура;
		КонецЕсли;

	КонецЦикла;

	Если СуммаМаксимальныхЧастот > 0 И ИнформацияОПроцессоре.КоличествоПроцессоров > 0 Тогда
		МаксимальнаяЧастота = СуммаМаксимальныхЧастот / ИнформацияОПроцессоре.КоличествоПроцессоров;
		ИнформацияОПроцессоре.МаксимальнаяЧастота = КонвертацияЗначенийCpuInfo.МегагерцыВГерцы(МаксимальнаяЧастота);
	КонецЕсли;

	ИнформацияОПроцессоре.НоминальнаяЧастота = ИнформацияОПроцессоре.МаксимальнаяЧастота;
	ИнформацияОПроцессоре.ИмяПроцессора = СтрСоединить(ИменаМоделей, ", ");

	Возврат ИнформацияОПроцессоре;
	
КонецФункции

Процедура ИсправитьВыводWmic(Текст)

	Если Не ЗначениеЗаполнено(Текст) Или СтрЧислоСтрок(Текст) <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	ПерваяСтрока = СтрПолучитьСтроку(Текст, 1);
	ВтораяСтрока = СтрПолучитьСтроку(Текст, 2);
	ВКПС = Символы.ВК + Символы.ПС;
	
	Если ЗначениеЗаполнено(ПерваяСтрока) И Не ЗначениеЗаполнено(ВтораяСтрока) Тогда
		Текст = СтрЗаменить(Текст, ВКПС + ВКПС, ВКПС);
	КонецЕсли;

КонецПроцедуры

Функция АрхитектураПоКоду(Код)
	
	Если Код = "0" Тогда
		Возврат АрхитектурыПроцессоров.X86;
	ИначеЕсли Код = "5" Тогда
		Возврат АрхитектурыПроцессоров.ARM;
	ИначеЕсли Код = "9" Тогда
		Возврат АрхитектурыПроцессоров.X64;
	ИначеЕсли Код = "12" Тогда
		Возврат АрхитектурыПроцессоров.ARM64;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции