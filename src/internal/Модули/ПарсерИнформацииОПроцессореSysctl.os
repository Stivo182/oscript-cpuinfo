Функция Распарсить(ВыводSysctl) Экспорт
	
	ИнформацияОПроцессоре = Новый ИнформацияОПроцессоре(Ложь);

	Процессор = ПарсерСекцийИнформацииОПроцессоре.РаспарситьСекцию(ВыводSysctl, ":");

	ИмяПроцессора = Процессор["machdep.cpu.brand_string"];
	Если ЗначениеЗаполнено(ИмяПроцессора) Тогда
		ИнформацияОПроцессоре.ИмяПроцессора = ИмяПроцессора;
	КонецЕсли;

	ИнформацияОПроцессоре.КоличествоПроцессоров = КонвертацияЗначенийCpuInfo.СтрокаВЧисло(Процессор["hw.packages"]);
	ИнформацияОПроцессоре.КоличествоЯдер = КонвертацияЗначенийCpuInfo.СтрокаВЧисло(Процессор["hw.physicalcpu"]);
	ИнформацияОПроцессоре.КоличествоЛогическихПроцессоров = КонвертацияЗначенийCpuInfo.СтрокаВЧисло(Процессор["hw.logicalcpu"]);
	ИнформацияОПроцессоре.НоминальнаяЧастота = КонвертацияЗначенийCpuInfo.СтрокаВЧисло(Процессор["hw.cpufrequency"]);
	ИнформацияОПроцессоре.МаксимальнаяЧастота = КонвертацияЗначенийCpuInfo.СтрокаВЧисло(Процессор["hw.cpufrequency_max"]);
	ИнформацияОПроцессоре.Архитектура = ОпределитьАрхитектуру(Процессор["hw.cputype"], Процессор["hw.cpu64bit_capable"]);

	Возврат ИнформацияОПроцессоре;
	
КонецФункции

Функция ОпределитьАрхитектуру(Cputype, Cpu64bit_capable)

	Если Cputype = "7" Или Cputype = "16777223" Тогда
		Если Cpu64bit_capable = "1" Тогда
			Возврат АрхитектурыПроцессоров.X64;
		Иначе
			Возврат АрхитектурыПроцессоров.X86;
		КонецЕсли;		
	ИначеЕсли Cputype = "12" Или Cputype = "16777228" Тогда
		Если Cpu64bit_capable = "1" Тогда
			Возврат АрхитектурыПроцессоров.ARM64;
		Иначе
			Возврат АрхитектурыПроцессоров.ARM;	
		КонецЕсли;
	КонецЕсли;

КонецФункции