#Использовать 1commands

#Область ПрограммныйИнтерфейс

Функция Доступен() Экспорт
	
	Тип = Новый СистемнаяИнформация().ТипПлатформы;
	Возврат Тип = ТипПлатформы.Windows_x86 Или Тип = ТипПлатформы.Windows_x86_64;

КонецФункции

Функция Определить() Экспорт

	Если Не Доступен() Тогда
		ВызватьИсключение "Детектор доступен только на Windows";
	КонецЕсли;

	Результат = ВыводCIM();
	Если Не Результат = Неопределено Тогда
		Возврат ПарсерИнформацииОПроцессореCIM.Распарсить(Результат);
	КонецЕсли;

	Результат = ВыводWmic();
	Если Не Результат = Неопределено Тогда
		Возврат ПарсерИнформацииОПроцессореWmic.Распарсить(Результат);
	КонецЕсли;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурИФункции

Функция ВыводWmic()

	Команда = Новый Команда;

	ПутьКWmic = "C:\Windows\System32\wbem\WMIC.exe";
	Если Новый Файл(ПутьКWmic).Существует() Тогда
		Команда.УстановитьКоманду(ПутьКWmic);
	Иначе
		Команда.УстановитьКоманду("wmic");
	КонецЕсли;

	Команда.ДобавитьПараметр("cpu get Architecture, Name, NumberOfCores, NumberOfLogicalProcessors, MaxClockSpeed /Format:List");

	КодВозврата = Команда.Исполнить();
	Если КодВозврата = 0 Тогда
		Возврат Команда.ПолучитьВывод();
	КонецЕсли;

КонецФункции

Функция ВыводCIM()

	ПутьКPS = "C:\Windows\System32\WindowsPowerShell\v1.0\powershell.exe";
	Если Не Новый Файл(ПутьКPS).Существует() Тогда
		Возврат Неопределено;
	КонецЕсли;

	// Используется "Команда" вместо "КомандныйФайл", так как выполнение сценариев может быть отключено в системе.
	Команда = Новый Команда;
	Команда.УстановитьКоманду(ПутьКPS);
	Команда.ДобавитьПараметр("""Get-CimInstance Win32_Processor | Format-List Architecture, Name, NumberOfCores, NumberOfLogicalProcessors, MaxClockSpeed""");

	КодВозврата = Команда.Исполнить();
	Если КодВозврата = 0 Тогда
		Возврат Команда.ПолучитьВывод();
	КонецЕсли;

КонецФункции

#КонецОбласти