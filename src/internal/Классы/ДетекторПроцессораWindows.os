#Использовать 1commands
#Использовать fs

Перем _ПутьКWmic; // Строка
Перем _ПутьКPowershell; // Строка

Процедура ПриСозданииОбъекта()

	_ПутьКWmic = "C:\Windows\System32\wbem\WMIC.exe";
	_ПутьКPowershell = "C:\Windows\System32\WindowsPowerShell\v1.0\powershell.exe";

КонецПроцедуры

#Область ПрограммныйИнтерфейс

Функция Доступен() Экспорт
	
	Тип = Новый СистемнаяИнформация().ТипПлатформы;
	Возврат Тип = ТипПлатформы.Windows_x86 Или Тип = ТипПлатформы.Windows_x86_64;

КонецФункции

Функция Определить() Экспорт

	Если Не Доступен() Тогда
		ВызватьИсключение "Детектор доступен только на Windows";
	КонецЕсли;

	// Команда через WMIC выполняется быстрее
	Если ФС.ФайлСуществует(_ПутьКPowershell) И Не ФС.ФайлСуществует(_ПутьКWmic) Тогда
		Возврат ПарсерИнформацииОПроцессореCim.Распарсить(ВыводCim());
	Иначе
		Возврат ПарсерИнформацииОПроцессореWmic.Распарсить(ВыводWmic());
	КонецЕсли;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурИФункции

Функция ВыводWmic()

	Команда = Новый Команда;
	Команда.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
	Команда.УстановитьПравильныйКодВозврата(0);

	Если ФС.ФайлСуществует(_ПутьКWmic) Тогда
		Команда.УстановитьКоманду(_ПутьКWmic);
	Иначе
		Команда.УстановитьКоманду("wmic");
	КонецЕсли;

	Команда.ДобавитьПараметр("cpu get Architecture, Name, NumberOfCores, NumberOfLogicalProcessors, MaxClockSpeed /Format:List");
	Команда.Исполнить();

	Возврат Команда.ПолучитьВывод();

КонецФункции

Функция ВыводCim()

	// Используется "Команда" вместо "КомандныйФайл", так как выполнение сценариев может быть отключено в системе.
	Команда = Новый Команда;
	Команда.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
	Команда.УстановитьПравильныйКодВозврата(0);
	Команда.УстановитьКоманду(_ПутьКPowershell);
	Команда.ДобавитьПараметр("""Get-CimInstance Win32_Processor | Format-List Architecture, Name, NumberOfCores, NumberOfLogicalProcessors, MaxClockSpeed""");
	Команда.Исполнить();
	
	Возврат Команда.ПолучитьВывод();

КонецФункции

#КонецОбласти