#Использовать 1commands

Функция Доступен() Экспорт
	
	Тип = Новый СистемнаяИнформация().ТипПлатформы;
	Возврат Тип = ТипПлатформы.Windows_x86 Или Тип = ТипПлатформы.Windows_x86_64;

КонецФункции

Функция Определить() Экспорт

	Если Не Доступен() Тогда
		ВызватьИсключение "Детектор доступен только на Windows";
	КонецЕсли;

	Возврат ПарсерИнформацииОПроцессореWmic.Распарсить(ВыводWmic());

КонецФункции

Функция ВыводWmic()

	Команда = Новый Команда;

	ПутьКWmic = "C:\Windows\System32\wbem\WMIC.exe";
	Если Новый Файл(ПутьКWmic).Существует() Тогда
		Команда.УстановитьКоманду(ПутьКWmic);
	Иначе
		Команда.УстановитьКоманду("wmic");
	КонецЕсли;

	Команда.ДобавитьПараметр("cpu get Name, NumberOfCores, NumberOfLogicalProcessors, MaxClockSpeed /Format:List");
	Команда.Исполнить();

	Возврат Команда.ПолучитьВывод();

КонецФункции