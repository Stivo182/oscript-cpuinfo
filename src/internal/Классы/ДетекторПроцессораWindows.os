#Использовать 1commands
#Использовать fs

Перем _ПутьКWmic; // Строка
Перем _ПутьКPowershell; // Строка

Процедура ПриСозданииОбъекта()
	_ПутьКWmic = "C:\Windows\System32\wbem\WMIC.exe";
	_ПутьКPowershell = "C:\Windows\System32\WindowsPowerShell\v1.0\powershell.exe";
КонецПроцедуры

#Область ПрограммныйИнтерфейс

Функция Доступен() Экспорт
	
	Тип = Новый СистемнаяИнформация().ТипПлатформы;
	Возврат Тип = ТипПлатформы.Windows_x86 Или Тип = ТипПлатформы.Windows_x86_64;

КонецФункции

Функция Определить() Экспорт

	Если Не Доступен() Тогда
		ВызватьИсключение "Детектор доступен только на Windows";
	КонецЕсли;

	// Команда через WMIC выполняется быстрее
	Если ФС.ФайлСуществует(_ПутьКWmic) Тогда
		ИнформацияОПроцессоре = ИнформацияОПроцессореЧерезWmic();
	ИначеЕсли ФС.ФайлСуществует(_ПутьКPowershell) Тогда
		ИнформацияОПроцессоре = ИнформацияОПроцессореЧерезCim();
	Иначе
		ИнформацияОПроцессоре = ИнформацияОПроцессореЧерезWmic();
	КонецЕсли;

	Возврат ИнформацияОПроцессоре;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурИФункции

Функция ИнформацияОПроцессореЧерезWmic()
	
	Результат = ВыводWmic();
	Если Не Результат = Неопределено Тогда
		Возврат ПарсерИнформацииОПроцессореWmic.Распарсить(Результат);
	КонецЕсли;

КонецФункции

Функция ИнформацияОПроцессореЧерезCim()
	
	Результат = ВыводCim();
	Если Не Результат = Неопределено Тогда
		Возврат ПарсерИнформацииОПроцессореCim.Распарсить(Результат);
	КонецЕсли;

КонецФункции

Функция ВыводWmic()

	Команда = Новый Команда;

	Если ФС.ФайлСуществует(_ПутьКWmic) Тогда
		Команда.УстановитьКоманду(_ПутьКWmic);
	Иначе
		Команда.УстановитьКоманду("wmic");
	КонецЕсли;

	Команда.ДобавитьПараметр("cpu get Architecture, Name, NumberOfCores, NumberOfLogicalProcessors, MaxClockSpeed /Format:List");

	КодВозврата = Команда.Исполнить();
	Если КодВозврата = 0 Тогда
		Возврат Команда.ПолучитьВывод();
	КонецЕсли;

КонецФункции

Функция ВыводCim()

	Если Не ФС.ФайлСуществует(_ПутьКPowershell) Тогда
		Возврат Неопределено;
	КонецЕсли;

	// Используется "Команда" вместо "КомандныйФайл", так как выполнение сценариев может быть отключено в системе.
	Команда = Новый Команда;
	Команда.УстановитьКоманду(_ПутьКPowershell);
	Команда.ДобавитьПараметр("""Get-CimInstance Win32_Processor | Format-List Architecture, Name, NumberOfCores, NumberOfLogicalProcessors, MaxClockSpeed""");

	КодВозврата = Команда.Исполнить();
	Если КодВозврата = 0 Тогда
		Возврат Команда.ПолучитьВывод();
	КонецЕсли;

КонецФункции

#КонецОбласти