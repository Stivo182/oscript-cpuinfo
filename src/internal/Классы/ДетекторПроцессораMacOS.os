#Использовать 1commands

Функция Доступен() Экспорт
	
	Тип = Новый СистемнаяИнформация().ТипПлатформы;
	Возврат Тип = ТипПлатформы.MacOS_x86 Или Тип = ТипПлатформы.MacOS_x86_64;

КонецФункции

Функция Определить() Экспорт

	Если Не Доступен() Тогда
		ВызватьИсключение "Детектор доступен только на MacOS";
	КонецЕсли;

	Sysctl = ВыводSysctl();
	ProcTranslated = ВыводProcTranslated();
	ЭтоЭмуляцияХ64 = ProcTranslated = "1";

	Возврат ПарсерИнформацииОПроцессореSysctl.Распарсить(Sysctl, ЭтоЭмуляцияХ64);

КонецФункции

Функция ВыводSysctl()

	Команда = Новый Команда;
	Команда.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
	Команда.УстановитьПравильныйКодВозврата(0);
	Команда.УстановитьКоманду("sysctl");
	Команда.ДобавитьПараметр("-a");
	Команда.Исполнить();

	Возврат Команда.ПолучитьВывод();

КонецФункции

Функция ВыводProcTranslated()

	Команда = Новый Команда;
	Команда.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
	Команда.УстановитьПравильныйКодВозврата(0);
	Команда.УстановитьКоманду("sysctl");
	Команда.ДобавитьПараметр("-n");
	Команда.ДобавитьПараметр("sysctl.proc_translated");
	Команда.Исполнить();

	Возврат СокрЛП(Команда.ПолучитьВывод());

КонецФункции