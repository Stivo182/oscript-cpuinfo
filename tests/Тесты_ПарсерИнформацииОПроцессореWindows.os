#Использовать "helpers"

&Тест
Процедура ПустойТест() Экспорт
	
	ОжидаемоеЗначение = Новый ИнформацияОПроцессоре(Ложь);

	ПроверяемоеЗначение = ПарсерИнформацииОПроцессореWmic.Распарсить("");

	ТестированиеИнформацииОПроцессоре.ПроверитьРавенство(ПроверяемоеЗначение, ОжидаемоеЗначение);

КонецПроцедуры

&Тест
Процедура НеверноеФорматирование() Экспорт
	
	ОжидаемоеЗначение = Новый ИнформацияОПроцессоре(Ложь);

	ПроверяемоеЗначение = ПарсерИнформацииОПроцессореWmic.Распарсить("malformedkey: malformedvalue\n\nmalformedkey2: malformedvalue2");

	ТестированиеИнформацииОПроцессоре.ПроверитьРавенство(ПроверяемоеЗначение, ОжидаемоеЗначение);

КонецПроцедуры

&Тест
Процедура ДваПроцессораПоВосемьЯдер() Экспорт
	
	ОжидаемоеЗначение = Новый ИнформацияОПроцессоре(Ложь);
	ОжидаемоеЗначение.ИмяПроцессора = "Intel(R) Xeon(R) CPU E5-2630 v3 @ 2.40GHz";
	ОжидаемоеЗначение.КоличествоПроцессоров = 2;
	ОжидаемоеЗначение.КоличествоЯдер = 16;
	ОжидаемоеЗначение.КоличествоЛогическихПроцессоров = 32;
	ОжидаемоеЗначение.НоминальнаяЧастота = 2400000000;
	ОжидаемоеЗначение.МаксимальнаяЧастота = 2400000000;
	ОжидаемоеЗначение.Архитектура = АрхитектурыПроцессоров.X64;

	ТекстоваяИнформация = "
	|Architecture=9
	|MaxClockSpeed=2400
	|Name=Intel(R) Xeon(R) CPU E5-2630 v3 @ 2.40GHz
	|NumberOfCores=8
	|NumberOfLogicalProcessors=16
	|
	|
	|Architecture=9
	|MaxClockSpeed=2400
	|Name=Intel(R) Xeon(R) CPU E5-2630 v3 @ 2.40GHz
	|NumberOfCores=8
	|NumberOfLogicalProcessors=16";

	ПроверяемоеЗначение = ПарсерИнформацииОПроцессореWmic.Распарсить(ТекстоваяИнформация);

	ТестированиеИнформацииОПроцессоре.ПроверитьРавенство(ПроверяемоеЗначение, ОжидаемоеЗначение);

КонецПроцедуры

&Тест
Процедура ДваПроцессораПоВосемьЯдерСБагомWmic() Экспорт
	
	ОжидаемоеЗначение = Новый ИнформацияОПроцессоре(Ложь);
	ОжидаемоеЗначение.ИмяПроцессора = "Intel(R) Xeon(R) CPU E5-2687W 0 @ 3.10GHz";
	ОжидаемоеЗначение.КоличествоПроцессоров = 2;
	ОжидаемоеЗначение.КоличествоЯдер = 16;
	ОжидаемоеЗначение.КоличествоЛогическихПроцессоров = 32;
	ОжидаемоеЗначение.НоминальнаяЧастота = 3111000000;
	ОжидаемоеЗначение.МаксимальнаяЧастота = 3111000000;

	ТекстоваяИнформация = 
	"\r\r\n" +
	"\r\r\n" +
	"MaxClockSpeed=3111\r\r\n" +
	"Name=Intel(R) Xeon(R) CPU E5-2687W 0 @ 3.10GHz\r\r\n" +
	"NumberOfCores=8\r\r\n" +
	"NumberOfLogicalProcessors=16\r\r\n" +
	"\r\r\n" +
	"\r\r\n" +
	"MaxClockSpeed=3111\r\r\n" +
	"Name=Intel(R) Xeon(R) CPU E5-2687W 0 @ 3.10GHz\r\r\n" +
	"NumberOfCores=8\r\r\n" +
	"NumberOfLogicalProcessors=16\r\r\n" +
	"\r\r\n" +
	"\r\r\n" +
	"\r\r\n";
	ТекстоваяИнформация = СтрЗаменить(ТекстоваяИнформация, "\n", Символы.ПС);
	ТекстоваяИнформация = СтрЗаменить(ТекстоваяИнформация, "\r", Символы.ВК);

	ПроверяемоеЗначение = ПарсерИнформацииОПроцессореWmic.Распарсить(ТекстоваяИнформация);

	ТестированиеИнформацииОПроцессоре.ПроверитьРавенство(ПроверяемоеЗначение, ОжидаемоеЗначение);

КонецПроцедуры

&Тест
Процедура ОдинПроцессорЧетыреЯдра() Экспорт
	
	ОжидаемоеЗначение = Новый ИнформацияОПроцессоре(Ложь);
	ОжидаемоеЗначение.ИмяПроцессора = "Intel(R) Core(TM) i7-4710MQ CPU @ 2.50GHz";
	ОжидаемоеЗначение.КоличествоПроцессоров = 1;
	ОжидаемоеЗначение.КоличествоЯдер = 4;
	ОжидаемоеЗначение.КоличествоЛогическихПроцессоров = 8;
	ОжидаемоеЗначение.НоминальнаяЧастота = 2500000000;
	ОжидаемоеЗначение.МаксимальнаяЧастота = 2500000000;
	ОжидаемоеЗначение.Архитектура = АрхитектурыПроцессоров.X64;

	ТекстоваяИнформация = "
	|Architecture=9
	|MaxClockSpeed=2500
	|Name=Intel(R) Core(TM) i7-4710MQ CPU @ 2.50GHz
	|NumberOfCores=4
	|NumberOfLogicalProcessors=8";

	ПроверяемоеЗначение = ПарсерИнформацииОПроцессореWmic.Распарсить(ТекстоваяИнформация);

	ТестированиеИнформацииОПроцессоре.ПроверитьРавенство(ПроверяемоеЗначение, ОжидаемоеЗначение);

КонецПроцедуры

&Тест
Процедура ТестАрхитектуры() Экспорт

	ТестовыеСлучаи = Новый Соответствие();
	ТестовыеСлучаи.Вставить("Architecture=0", АрхитектурыПроцессоров.X86);
	ТестовыеСлучаи.Вставить("Architecture=5", АрхитектурыПроцессоров.ARM);
	ТестовыеСлучаи.Вставить("Architecture=9", АрхитектурыПроцессоров.X64);
	ТестовыеСлучаи.Вставить("Architecture=12", АрхитектурыПроцессоров.ARM64);
	ТестовыеСлучаи.Вставить("", Неопределено);
	ТестовыеСлучаи.Вставить("Architecture=99", Неопределено);
	
	Для Каждого ТестовыйСлучай Из ТестовыеСлучаи Цикл

		ВыводWmic = ТестовыйСлучай.Ключ;

		ОжидаемоеЗначение = Новый ИнформацияОПроцессоре(Ложь);
		ОжидаемоеЗначение.Архитектура = ТестовыйСлучай.Значение;
	
		ПроверяемоеЗначение = ПарсерИнформацииОПроцессореWmic.Распарсить(ВыводWmic);

		ТестированиеИнформацииОПроцессоре.ПроверитьРавенство(ПроверяемоеЗначение, ОжидаемоеЗначение, ВыводWmic);

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ОдинПроцессорШестьЯдерCIM() Экспорт
	
	ОжидаемоеЗначение = Новый ИнформацияОПроцессоре(Ложь);
	ОжидаемоеЗначение.ИмяПроцессора = "Intel(R) Core(TM) i5-8500 CPU @ 3.00GHz";
	ОжидаемоеЗначение.КоличествоПроцессоров = 1;
	ОжидаемоеЗначение.КоличествоЯдер = 6;
	ОжидаемоеЗначение.КоличествоЛогическихПроцессоров = 6;
	ОжидаемоеЗначение.НоминальнаяЧастота = 3000000000;
	ОжидаемоеЗначение.МаксимальнаяЧастота = 3000000000;
	ОжидаемоеЗначение.Архитектура = АрхитектурыПроцессоров.X64;

	ТекстоваяИнформация = "Architecture              : 9
	|Name                      : Intel(R) Core(TM) i5-8500 CPU @ 3.00GHz
	|NumberOfCores             : 6
	|NumberOfLogicalProcessors : 6
	|MaxClockSpeed             : 3000
	|
	|
	|
	|";

	ПроверяемоеЗначение = ПарсерИнформацииОПроцессореCIM.Распарсить(ТекстоваяИнформация);

	ТестированиеИнформацииОПроцессоре.ПроверитьРавенство(ПроверяемоеЗначение, ОжидаемоеЗначение);

КонецПроцедуры
